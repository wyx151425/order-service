plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2021.0.3")
    set('testcontainersVersion', "1.17.3")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.retry:spring-retry'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    // 为 OkHttp MockWebServer 添加测试依赖
    testImplementation 'com.squareup.okhttp3:mockwebserver'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:r2dbc'

    // 提供通过迁移实现数据库版本管理的功能
    runtimeOnly 'org.flywaydb:flyway-core'
    // 提供 JDBC 驱动，以允许应用连接至 PostgreSQL 数据库
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'
    // 为 Spring 提供使用 JDBC API 进行集成的功能。它是 Spring 框架的一部分，不要与 Spring Data JDBC 混淆
    runtimeOnly 'org.springframework:spring-jdbc'

    implementation 'org.springframework.cloud:spring-cloud-stream-binder-rabbit'
    testImplementation("org.springframework.cloud:spring-cloud-stream") {
        artifact {
            name = "spring-cloud-stream"
            extension = "jar"
            type ="test-jar"
            classifier = "test-binder"
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// 使用 Buildpacks 构建 Docker 镜像的 Spring Boot Plugin task
bootBuildImage {
    // 要构建的 OCI 镜像的名称。与项目 Gradle 配置中的名称相同。当在本地运行时，我们使用隐式的 “latest” 作为标签，而没有使用版本号
    imageName = "${project.name}"
    // 在镜像中安装的 JVM 版本，这里使用的是最新的 Java 17 版本
    environment = ["BP_JVM_VERSION": "17.*"]

    docker {
        // 该部分配置与容器注册中心的连接
        publishRegistry {
            // 该部分配置发布容器注册中心的认证。这些值会以 Gradle 属性的形式传递进来
            // ./gradlew bootBuildImage --imageName ghcr.io/wyx151425/catalog-service --publishImage -PregistryUrl=ghcr.io -PregistryUsername=wyx151425 -PregistryToken=ghp_
            username = project.findProperty("registryUsername")
            password = project.findProperty("registryToken")
            url = project.findProperty("registryUrl")
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
